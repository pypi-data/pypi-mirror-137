start: message+

message: [aftn_data] flight_plan
flight_plan: "(FPL-" field_7 "-" field_8 "-" field_9 "-" field_10 "-" field_13 "-" field_15 "-" field_16 ["-" field_18 ["-" field_19*]] ")" -> fpl

aftn_data: FLIGHT_PRIORITY recipients DTG_SHORT [ADDRESS]
field_7: CALLSIGN
field_8: FLIGHT_RULES FLIGHT_TYPE
field_9: [AIRCRAFT_NUM] AIRCRAFT_TYPE "/" WAKE
field_10: EQUIPMENT "/" SURVEILLANCE_EQUIPMENT
field_13: ICAO TIME
field_15: SPEED ALT route
field_16: ICAO TIME [ICAO~1..2]
field_18: supplemental_data*
field_19: ["E/" DIGIT~4] ["P/" DIGIT~1..3] emergency_equipment*

recipients: ADDRESS+

supplemental_data: DATA_TYPE "/" DATA_VALUE
DATA_TYPE: "SUR"|"EET"|"REG"|"RIF"|"SEL"|"OPR"|"STS"|"TYP"|"PBN"|"PER"|"COM"|"DAT"|"NAV"|"DEP"|"DEST"|"ALTN"|"ORGN"|"RALT"|"CODE"|"RMK"|"RVR"|"DOF"
DATA_VALUE: /[^\/)]+?(?=\s\w{3,5}\/|\))/

emergency_equipment: "R/" LETTER+

// TODO: Implement all route fields.
route: route_point+

?route_point: (CODEPOINT | COORDINATE) ("/" SPEED ALT)? (LETTER~3)?
| "C/" COORDINATE "/" SPEED ALT (ALT | "PLUS") -> cruise_climb

CODEPOINT: ALPHANUM~2..8

FLIGHT_PRIORITY: ("SS"|"DD"|"FF"|"GG"|"KK")
ADDRESS: LETTER~4 LETTER~3 LETTER

FLIGHT_RULES: LETTER
FLIGHT_TYPE: LETTER

AIRCRAFT_NUM: DIGIT~1..2
AIRCRAFT_TYPE: ALPHANUM~2..4

EQUIPMENT: ("N"|"S") ALPHANUM*
SURVEILLANCE_EQUIPMENT: LETTER~1..2

SPEED: ("K"|"N"|"M") DIGIT~3..4
ALT: ("F"|"S"|"A"|"M") DIGIT~3..4

WAKE: ("H"|"M"|"L"|"J")
CALLSIGN: ALPHANUM~5..7
ICAO: LETTER~4
DTG_SHORT: DIGIT~2 TIME
TIME: DIGIT~4
COORDINATE: (COORDINATE_DEG | COORDINATE_DEGMIN)
COORDINATE_DEG: DIGIT~2 ("N"|"S") DIGIT~3 ("E"|"W")
COORDINATE_DEGMIN: DIGIT~4 ("N"|"S") DIGIT~5 ("E"|"W")
ALPHANUM: (LETTER|DIGIT)

%import common (WS, LETTER, DIGIT, CNAME)
%ignore WS

